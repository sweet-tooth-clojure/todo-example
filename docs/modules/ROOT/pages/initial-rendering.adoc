= Initial Rendering =
Daniel Higginbotham



What's involved in displaying the initial form and sidebar? If you
look in the `sweet-tooth.todo-example.frontend.core` namespace, you'll
see:

[source,clojure]
.initial rendering fns
----
(defn system-config
  "This is a function instead of a static value so that it will pick up
  reloaded changes"
  []
  (mm/meta-merge stconfig/default-config
                 {::stfr/frontend-router {:use    :reitit
                                          :routes froutes/frontend-routes}
                  ::stfr/sync-router     {:use    :reitit
                                          :routes (ig/ref ::eroutes/routes)}

                  ;; Treat handler registration as an external service,
                  ;; interact with it via re-frame effects
                  ::stjehf/handlers {}
                  ::eroutes/routes  ""}))

(defn -main []
  (rf/dispatch-sync [::stcf/init-system (system-config)])
  (rf/dispatch-sync [::stnf/dispatch-current])
  (r/render [app/app] (stcu/el-by-id "app")))
----

Most of this will be unfamiliar, but you look at the very last line
you'll see some code you might recognize:

[source,clojure]
.here's where we render
----
(r/render [app/app] (stcu/el-by-id "app"))
----

We're rendering the `app/app` component to the `<div id="app"></div>` DOM
element. Here's what the `app/app` component looks like:

[source,clojure]
.the `app/app` component
----
(defn app
  []
  [:div.app
   [:div.head
    [:div.container [:a {:href (stfr/path :home)} "Wow! A To-Do List!"]]]
   [:div.container.grid
    [:div.side @(rf/subscribe [::stnf/routed-component :side])]
    [:div.main @(rf/subscribe [::stnf/routed-component :main])]]])
----

Hmm. There's still nothing here that looks like the forms we see in
the sidebar and main area. What's going on? Here's the high level
overview, which I'll explain in detail in the sections that follow:

. Integrant initializes system components
. One component is a _router_ that associates URL patterns with
+
** What components to display
** Lifecycle callbacks that should get dispatched on entering or
exiting a route
+
(I haven't shown the router code that ties routes to components and
lifecycle callbacks, but I'll introduce you to it later.)
. Another component is a _nav handler_ that reacts to nav events by
looking up the corresponding _route_, dispatching its lifecycle,
and setting it as the current route in the appdb
. The `[::stnf/dispatch-current]` re-frame event causes the nav
handler to handle the current URL, dispatching its lifecycle and
setting the current route
. The `::stnf/routed-component` subscription pulls components for the
current route out of the app db, and those components get rendered

Now let's go through all this in detail.


== App Initialization ==
When you open the home page, the app renders the home page's
components. For most projects, your app must perform some kind of
initialization process (to set up route handling, for example) to get
to this point. How does a Sweet Tooth app do it?

Briefly, Sweet Tooth provides a re-frame handler to initialize an https://github.com/weavejester/integrant[Integrant]
_system_ (check out the Integrant docs for a description of what a system is and
how Integrant provides a mechanism for starting and stopping components). The
system includes a component for managing nav events, like loading the initial
page or clicking a link. This nav component looks up the _route_ for the current
URL in a https://github.com/metosin/reitit[reitit] router. The route defines _lifecycle callbacks_ and also defines
which high-level components should get displayed.

We'll look at each of these parts of the framework and how we use them
in our app.

First, let's look at the `sweet-tooth.todo-example.frontend.core`
namespace again:

[source,clojure]
.system config
----
(defn system-config
  "This is a function instead of a static value so that it will pick up
  reloaded changes"
  []
  (mm/meta-merge stconfig/default-config
                 {::stfr/frontend-router {:use    :reitit
                                          :routes froutes/frontend-routes}
                  ::stfr/sync-router     {:use    :reitit
                                          :routes (ig/ref ::eroutes/routes)}

                  ;; Treat handler registration as an external service,
                  ;; interact with it via re-frame effects
                  ::stjehf/handlers {}
                  ::eroutes/routes  ""}))

(defn -main []
  (rf/dispatch-sync [::stcf/init-system (system-config)])
  (rf/dispatch-sync [::stnf/dispatch-current])
  (r/render [app/app] (stcu/el-by-id "app")))
----

As is tradition for Lispers, let's start at the bottom and work our
way up. In the `-main` function, you can see we're dispatching two
events and then rendering a component. Let's walk through the
mechanics of what's going on, and then we'll talk about why it works
the way it does.

The first event is:

[source,clojure]
.first event
----
(rf/dispatch-sync [::stcf/init-system (system-config)])
----

`(system-config)` returns an Integrant config, a map describing a
system where each key corresponds to the name of a _system component_
(as opposed to a React compononent) and each value is that component's
configuration. Examples of system components include nav handlers that
react to History events and web worker managers.

Sweet Tooth comes with a bunch o' system components that are meant to make your
life easier, and the default config for those components lives at
`stconfig/default-config`. In the `system-config` function we merge the default
Sweet Tooth config with our app's particular config. We use https://github.com/weavejester/meta-merge[`meta-merge`] because
of its support for deep merging and because of how it gives you some control
over how the two values get merged.

This system config is the payload for the `::stcf/init-system` event. This event
https://github.com/sweet-tooth-clojure/frontend/blob/master/src/sweet_tooth/frontend/core/flow.cljc#L123[results in integrant getting called] to _initialize_ the system:

[source,clojure]
----
(rf/reg-event-fx ::init-system
  (fn [_ [_ config]]
    {::init-system config}))

(rf/reg-fx ::init-system
  (fn [config]
    (reset! rfdb/app-db {:sweet-tooth/system (-> config
                                                 ig/prep
                                                 ig/init)})))
----

Integrant initializes an app by initializing individual components in
dependency order; the nav handler component depends on a router
component, so the router gets initialized before the nav handler.

Why do we use Integrant to initialize our app? A few reasons:

. Sometimes we want to render different React components at different
stages of the system's readiness. For example, you might want to
show a loading indicator while the app sets up whatever state is
necessary for it to be used, and then render the app proper once
the system is ready. Integrant makes it a lot easier to determine
when the system is ready.
. Integrant has a very simple model for handling both initializing
_and_ halting a system. This is very useful for local development
with livereload when you have components that modify global state,
for example by attaching event listeners to the window. Livereload
can call `(ig/halt!)` on the system, giving each component to clean
up after itself (remove its listeners) before code gets reloaded.
. Integrant makes it easier to code to interfaces. The nav handler
component depends on a router, and by default it depends on a
reitit router. However, you could provide a bidi or silk router
instead, as long as it can conform to the same interface. (This
isn't particular to the initialization process per se but I threw
it in because why not!?)

So that explains Integrant and how it fits into the app initialization
process, the first step in the `-main` function:

[source,clojure]
----
(defn -main []
  (rf/dispatch-sync [::stcf/init-system (system-config)])
  (rf/dispatch-sync [::stnf/dispatch-current])
  (r/render [app/app] (stcu/el-by-id "app")))
----

To understand the next step, `(rf/dispatch-sync
[::stnf/dispatch-current])`, we'll take a closer look at Sweet Tooth's
_nav handler_ component.


== The nav handler component ==
You can see the nav handler's default config in the
https://github.com/sweet-tooth-clojure/frontend/blob/master/src/sweet_tooth/frontend/config.cljs[`sweet-tooth.frontend.config`] namespace:

[source,clojure]
----
{::stnf/handler {:dispatch-route-handler ::stnf/dispatch-route
                 :check-can-unload?      true
                 :router                 (ig/ref ::stfr/frontend-router)
                 :global-lifecycle       (ig/ref ::stnf/global-lifecycle)}}
----

On initialization, it https://github.com/sweet-tooth-clojure/frontend/blob/master/src/sweet_tooth/frontend/nav/flow.cljs#L26[uses an adapted version of the accountant library] to
register javascript event handlers for nav events. These _javascript event_
handlers will dispatch _re-frame events_; Sweet Tooth's default configuration,
above, has the js event handlers dispatching the `::stnf/disptach-route`
re-frame event by default. In extremely simplified pseudocode, it's as if the
following gets evaluated when the nav component is initialized:

[source,clojure]
----
(js/listen js/NavEvent #(rf/dispatch [::stnf/dispatch-route]))
----

`::stnf/dispatch-route` is one of the gnarlier bits of Sweet Tooth,
and we don't need to go into all the details of how it works.
Ultimately what it does is:

. Figures out what _route_ corresponds to the potential new URL
proposed by the navigation event using a _router_. (I say
_potential_ URL because it's possible for nav events to get
rejected.)
. Dispatches the route's _lifecycle callbacks_
. Sets the currently active route in the re-frame app db

In the -main function, we see `(rf/dispatch-sync
[::stnf/dispatch-current])`. This behaves almost identically to
`::stnf/dispatch-route`; the only difference is that it operates on
the current URL.

To understand this process fully, we'll need to look at this router that I keep
talking about.


== The router component ==
I kept saying that the nav handler uses a router to look up
routes. Where does the router come from? You can see it in the config
for the nav handler:

[source,clojure]
----
{::stnf/handler {:dispatch-route-handler ::stnf/dispatch-route
                 :check-can-unload?      true
                 :router                 (ig/ref ::stfr/frontend-router) ;; <--- There it is!
                 :global-lifecycle       (ig/ref ::stnf/global-lifecycle)}}
----

The config includes a _reference_ to another component,
`::stfr/frontend-router`. We actually saw the configuration for _that_ component
in `sweet-tooth.todo-example.frontend.core`:

[source,clojure]
----
(defn system-config
  "This is a function instead of a static value so that it will pick up
  reloaded changes"
  []
  (mm/meta-merge stconfig/default-config
                 {::stfr/frontend-router {:use    :reitit
                                          :routes froutes/frontend-routes}
                  ::stfr/sync-router     {:use    :reitit
                                          :routes (ig/ref ::eroutes/routes)}

                  ;; Treat handler registration as an external service,
                  ;; interact with it via re-frame effects
                  ::stjehf/handlers {}
                  ::eroutes/routes  ""}))
----

So the `::stfr/frontend-router` component gets initialized with this
configuration:

[source,clojure]
----
{:use    :reitit
 :routes froutes/frontend-routes}
----

`:use` specifies what library should be used to parse route data into
a router, and reitit is supported out of the box. `:routes` specifies
the route data. Here's `froutes/frontend-routes`:

[source,clojure]
----
(ns sweet-tooth.todo-example.frontend.routes
  (:require [sweet-tooth.frontend.sync.flow :as stsf]
            [sweet-tooth.frontend.form.flow :as stff]
            [sweet-tooth.frontend.nav.flow :as stnf]
            [sweet-tooth.todo-example.cross.validate :as v]
            [sweet-tooth.todo-example.frontend.components.home :as h]
            [sweet-tooth.todo-example.frontend.components.todo-lists.list :as tll]
            [sweet-tooth.todo-example.frontend.components.todo-lists.show :as tls]
            [sweet-tooth.todo-example.frontend.components.ui :as ui]
            [clojure.spec.alpha :as s]
            [reitit.coercion.spec :as rs]))

(s/def :db/id int?)

(def frontend-routes
  [["/"
    {:name       :home
     :lifecycle  {:param-change [::stsf/sync-once [:get :todo-lists]]}
     :components {:side [tll/component]
                  :main [h/component]}
     :title      "To-Do List"}]

   ["/todo-list/{db/id}"
    {:name       :show-todo-list
     :lifecycle  {:param-change [[::stff/initialize-form [:todos :create] {:validate (ui/validate-with v/todo-rules)}]
                                 [::stsf/sync-once [:get :todo-lists]]
                                 [::stnf/get-with-route-params :todo-list]]}
     :components {:side [tll/component]
                  :main [tls/component]}
     :coercion   rs/coercion
     :parameters {:path (s/keys :req [:db/id])}
     :title      "To-Do List"}]])
----

You can see that each route has a `:components` key, a map with
`:side` and `:main` keys. When you load the home page, `tll/component`
shows up in the side bar, and `tls/component` shows up in the "main"
column.

At the beginning of all this I asked how the `app` component worked:

[source,clojure]
----
(defn app
  []
  [:div.app
   [:div.head
    [:div.container [:a {:href (stfr/path :home)} "Wow! A To-Do List!"]]]
   [:div.container.grid
    [:div.side @(rf/subscribe [::stnf/routed-component :side])]
    [:div.main @(rf/subscribe [::stnf/routed-component :main])]]])
----

Now we have all the pieces to solve the puzzle:

. A nav handler gets created on initialization
. It's passed a router that associates URL paths with components
. We dispatch `(rf/dispatch-sync [::stnf/dispatch-current])`. This
sets the current route in the re-frame app db.
. The `::stnf/routed-component` subscription looks up the
`:component` key for the current route in the app db.
. Those components get rendered.
